INCLUDE ~xpmod/functions/round_xp.tph~

DEFINE_ACTION_FUNCTION adjust_quest_xp
  INT_VAR
  percentage_modifier	= 100
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~xplist.2da~
  BEGIN
    COPY_EXISTING ~xplist.2da~ ~override~
      COUNT_2DA_COLS cols
      COUNT_2DA_ROWS cols rows
      READ_2DA_ENTRIES_NOW read_xplist cols
      FOR (SET y = 0; y < rows; SET y += 1)
      BEGIN
        FOR (SET x = 2; x < cols; SET x += 1)
        BEGIN
          READ_2DA_ENTRY_FORMER read_xplist y x xp
	  SPRINT party ~~
	  PATCH_IF ((~%xp%~ STRING_MATCHES_REGEXP ~P_[0-9]+~) == 0)
	  BEGIN
	    SPRINT party ~P_~
            INNER_PATCH_SAVE xp ~%xp%~
	    BEGIN
	      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~P_~ ~00~
	    END
	  END
          PATCH_IF (IS_AN_INT xp && xp > 0)
          BEGIN
            SET xp = xp * percentage_modifier / 100
            LAUNCH_PATCH_FUNCTION round_xp
              INT_VAR
              xp
              RET
              xp = round_xp
            END
          END
          SET_2DA_ENTRY_LATER write_xplist y x ~%party%%xp%~
        END
      END
      SET_2DA_ENTRIES_NOW write_xplist cols
      PRETTY_PRINT_2DA
    BUT_ONLY_IF_IT_CHANGES
  END
  
  
  COPY_EXISTING_REGEXP ~.+\.\(bcs\|dlg\)~ ~override~
    DECOMPILE_AND_PATCH
    BEGIN
      REPLACE_EVALUATE CASE_INSENSITIVE ~AddexperienceParty(\([0-9]+\))~
      BEGIN
        SET xp = MATCH1
        PATCH_IF (xp > 0)
        BEGIN
          SET xp = MATCH1 * percentage_modifier / 100
          LAUNCH_PATCH_FUNCTION round_xp
            INT_VAR
            xp
            RET
            xp = round_xp
          END
        END
      END ~AddexperienceParty(%xp%)~
  
      REPLACE_EVALUATE CASE_INSENSITIVE ~AddXPObject(\(.+\),\([0-9]+\))~
      BEGIN
        SET xp = MATCH2
        PATCH_IF (xp > 0)
        BEGIN
          SET xp = xp * percentage_modifier / 100
          LAUNCH_PATCH_FUNCTION round_xp
            INT_VAR
            xp
            RET
            xp = round_xp
          END
        END
      END ~AddXPObject(%MATCH1%, %xp%)~
    END        // DECOMPILE_AND_PATCH
  BUT_ONLY_IF_IT_CHANGES
  
  
  COPY_EXISTING_REGEXP ~.+\.spl~ ~override~
    GET_OFFSET_ARRAY ab_array 0x0064 4 0x0068 2 0x0000 0 0x0028
    PHP_EACH ab_array AS int => ab_offset
    BEGIN
      GET_OFFSET_ARRAY2 fx_array ab_offset 0x006a 4 0x001e 2 0x0020 2 0x0030
      PHP_EACH fx_array AS int => fx_offset
      BEGIN
        READ_SHORT (fx_offset + 0x0000) opcode
        READ_SLONG (fx_offset + 0x0004) xp
        READ_SLONG (fx_offset + 0x0008) type
        PATCH_IF (opcode == 104 && type == 0 && xp > 0)
        BEGIN
          SET xp = xp * percentage_modifier / 100
          LAUNCH_PATCH_FUNCTION round_xp
            INT_VAR
            xp
            RET
            xp = round_xp
          END
          WRITE_LONG (fx_offset + 0x0004) xp
        END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END


